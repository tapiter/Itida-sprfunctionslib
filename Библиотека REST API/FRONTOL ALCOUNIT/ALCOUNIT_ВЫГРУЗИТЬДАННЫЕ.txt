IF ( ПУСТО( _ИМЯТАБЛИЦЫ ) )
{
	СООБЩЕНИЕ("Не определена таблица товаров для выгрузки");
	RETURN false;
}

//признак необходимости замены алкокодов в товарах при выгрузке привязок алкокодов к товарам. true - заменять, false - добавлять
_ЗАМЕНЯТЬАЛКОКОДЫВТОВАРАХ = false;

//если код оборудования не определен, то инициализируем переменную с пустым значением
IF ( TYPE ("КОДОБОРУДОВАНИЯ") == "U" )
	КОДОБОРУДОВАНИЯ = "";

ИМЯОБОРУДОВАНИЯ = "";
ФИРМА = "";
СКЛАД = "";
ЛОГИН = "";
ПАРОЛЬ = "";
СЕРИЙНЫЙНОМЕР = "";
//если задан код оборудования, то определим из оборудования фирму, склад, наименование, серийный номер, логин, пароль
IF ( !ПУСТО( КОДОБОРУДОВАНИЯ ) )
{
	ДОБАВИТЬКОНТЕКСТ("SELECT name, firm, sklad, serialnumber, login, password, saldotype  FROM sprequipment WHERE code= '" + КОДОБОРУДОВАНИЯ + "'", "ДанныеОборудования", 1 );
	ИМЯОБОРУДОВАНИЯ = ДанныеОборудования.name;
	ФИРМА = ДанныеОборудования.firm;
	СКЛАД = ДанныеОборудования.sklad;
	ЛОГИН = ДанныеОборудования.login;
	ПАРОЛЬ = ДанныеОборудования.password;
	СЕРИЙНЫЙНОМЕР = ДанныеОборудования.serialnumber;
	//если в карточке оборудования выбран режим перезаписи остатков, то будем перезаписывать алкокоды в товарах при выгрузке
	_ЗАМЕНЯТЬАЛКОКОДЫВТОВАРАХ = ДанныеОборудования.saldotype == 1; 
}
	
IF ( !ПУСТО( _ИМЯТАБЛИЦЫ ) )
	ДОБАВИТЬКОНТЕКСТ( "SELECT sprnn.maincode, sprnn.ed, sprnn.nn, sprnn.name, dbo.fn_getbarcodes( sprnn.nn ) AS barcodes, sprnn.code AS Артикул, sprnn.nnvid AS ВидАлкоголя, sprnn.d_litr AS Объем, sprnn.edd_litr AS ЕдиницаОбъема, sprnn.weight AS Вес,
	sprnn.a_proc AS Крепость, dbo.fn_getalcocodes( sprnn.nn ) AS alcocodes, sprnn.importer AS Производитель, sprclient.shortname AS ПроизводительИмя, sprclient.inn AS ПроизводительИНН, sprclient.kpp AS ПроизводительКПП
					   FROM "  + _ИМЯТАБЛИЦЫ + " temp
					   INNER JOIN sprnn ON temp.nn = sprnn.nn
					   LEFT OUTER JOIN sprclient sprclient ON sprclient.code = sprnn.importer
					   WHERE sprnn.nnvid > 0", "СписокТоваров" );
ELSE IF ( !ПУСТО( КОДДОКУМЕНТА ) AND ПУСТО( СПИСОКДОКУМЕНТОВ ) )
	ДОБАВИТЬКОНТЕКСТ( "SELECT DISTINCT sprnn.maincode, sprnn.ed, sprnn.nn, sprnn.name, dbo.fn_getbarcodes( sprnn.nn ) AS barcodes, sprnn.code AS Артикул, sprnn.nnvid AS ВидАлкоголя, sprnn.d_litr AS Объем, sprnn.edd_litr AS ЕдиницаОбъема, sprnn.weight AS Вес,
	sprnn.a_proc AS Крепость, dbo.fn_getalcocodes( sprnn.nn ) AS alcocodes, sprnn.importer AS Производитель, sprclient.shortname AS ПроизводительИмя, sprclient.inn AS ПроизводительИНН, sprclient.kpp AS ПроизводительКПП
					   FROM spec"  + КОДДОКУМЕНТА + " spec
					   INNER JOIN sprnn ON spec.nn = sprnn.nn
					   LEFT OUTER JOIN sprclient sprclient ON sprclient.code = sprnn.importer
					   WHERE spec.ic = " + ИДДОКУМЕНТА + " AND sprnn.nnvid > 0", "СписокТоваров" );
ELSE IF ( !ПУСТО( СПИСОКДОКУМЕНТОВ ) )
	ДОБАВИТЬКОНТЕКСТ( "SELECT DISTINCT sprnn.maincode, sprnn.ed, sprnn.nn, sprnn.name, dbo.fn_getbarcodes( sprnn.nn ) AS barcodes, sprnn.code AS Артикул, sprnn.nnvid AS ВидАлкоголя, sprnn.d_litr AS Объем, sprnn.edd_litr AS ЕдиницаОбъема, sprnn.weight AS Вес,
	sprnn.a_proc AS Крепость, dbo.fn_getalcocodes( sprnn.nn ) AS alcocodes, sprnn.importer AS Производитель, sprclient.shortname AS ПроизводительИмя, sprclient.inn AS ПроизводительИНН, sprclient.kpp AS ПроизводительКПП
					   FROM spec000 spec
					   INNER JOIN " + СПИСОКДОКУМЕНТОВ + " temp ON temp.code = spec.code AND temp.ic = spec.ic
					   INNER JOIN sprnn ON spec.nn = sprnn.nn
					   LEFT OUTER JOIN sprclient sprclient ON sprclient.code = sprnn.importer
					   WHERE sprnn.nnvid > 0", "СписокТоваров" );
					   
АдресСервера = "";
//АдресСервера = "https://localhost:8443";
//определим адрес сервера Алкоюнит, а так же логин и пароль
//сперва будем брать адрес из серийного номера карточки оборудования
IF ( !ПУСТО( СЕРИЙНЫЙНОМЕР ) )
	АдресСервера = СЕРИЙНЫЙНОМЕР;

//если не задан в карточке оборудования или код оборудования не определен, то берем значение из константы _АЛКОЮНИТАДРЕССЕРВЕРА
IF ( ПУСТО( АдресСервера ) )
	АдресСервера = ПЕРЕМЕННАЯ( "_АЛКОЮНИТАДРЕССЕРВЕРА", "" );

//если не определен адрес и в константе, то берем локальный адрес
IF ( ПУСТО( АдресСервера ) )	
	АдресСервера = "https://localhost:8443"; //иначе обращаемся к локальному ПК
	
//определим логин и пароль из констант, если не задано в оборудовании. Если нет константы, то используем стандартное имя пользователя admin и пароль moo
IF ( ПУСТО(ЛОГИН) )
	ЛОГИН = ПЕРЕМЕННАЯ( "_АЛКОЮНИТЛОГИН", "admin" );
	
IF ( ПУСТО(ПАРОЛЬ) )
	ПАРОЛЬ = ПЕРЕМЕННАЯ( "_АЛКОЮНИТПАРОЛЬ", "moo" );
	
//выгружаем товары

ТекстЗапроса = "";
IF (КОЛИЧЕСТВОСТРОК("СписокТоваров") > 0 )
{
	ТекстЗапроса += "{" + CHR(13) + CHR(10);
	ТекстЗапроса += """products"": [" + CHR(13) + CHR(10);
}
	
ВЫБРАТЬКОНТЕКСТ("СписокТоваров");
ПЕРЕЙТИВНАЧАЛО("СписокТоваров");
WHILE (!КОНЕЦКОНТЕКСТА("СписокТоваров"))
{
	mAlco			= VAL( СписокТоваров.ВидАлкоголя ) > 0 AND VAL( СписокТоваров.ВидАлкоголя ) < 500 AND !ВСПИСКЕ( VAL( СписокТоваров.ВидАлкоголя ), 261, 262, 263 );
	ПивнаяПродукция	= VAL( СписокТоваров.ВидАлкоголя ) >= 500 OR ВСПИСКЕ( VAL( СписокТоваров.ВидАлкоголя ), 261, 262, 263 );

	ТекстЗапросаТовар = "{";
	ТекстЗапросаТовар += ДАННЫЕ_JSON("id", СписокТоваров.maincode) + ",";
	ТекстЗапросаТовар += ДАННЫЕ_JSON("name", СписокТоваров.name) + ",";
	ТекстЗапросаТовар += ДАННЫЕ_JSON("is_excise", mAlco) + ",";
	ТекстЗапросаТовар += """barcodes"": [""" + STRTRAN(СписокТоваров.barcodes,",",""",""") + """]";
	ТекстЗапросаТовар += "}";
		
	ТекстЗапроса += ЕСЛИ(ПРАВСИМВ(ТекстЗапроса, 1)=="}",",","") + ТекстЗапросаТовар;
	
	ПРОПУСТИТЬ( 1, "СписокТоваров" );
}
IF (КОЛИЧЕСТВОСТРОК("СписокТоваров") > 0 )
{
	ТекстЗапроса += "]" + CHR(13) + CHR(10);
	ТекстЗапроса += "}";
}

//отправим запрос с товарами, если он не пустой
IF (!ПУСТО(ТекстЗапроса))
{
	ТОКЕН = RESTAPI.ALCOUNIT_Авторизация(АдресСервера, ЛОГИН, ПАРОЛЬ);
	Ответ = RESTAPI.ALCOUNIT_POSTЗАПРОС(АдресСервера, "/products", ТекстЗапроса, ТОКЕН, "");
	ТекстЗапроса = "";
	//IF (!ПУСТО(Ответ))
	//	СООБЩЕНИЕ("Ответ сервера Alcounit:" + CHR(13) + CHR(10) + Ответ);
}

//пройдемся еще раз по контексту и выгрузим связи товаров с алкокодами
ТекстЗапроса = "";
IF (КОЛИЧЕСТВОСТРОК("СписокТоваров") > 0 )
{
	ТекстЗапроса += "{" + CHR(13) + CHR(10);
	ТекстЗапроса += """products"": [" + CHR(13) + CHR(10);
}

ВЫБРАТЬКОНТЕКСТ("СписокТоваров");
ПЕРЕЙТИВНАЧАЛО("СписокТоваров");
WHILE (!КОНЕЦКОНТЕКСТА("СписокТоваров"))
{
	ТекстЗапросаТовар = "{";
	ТекстЗапросаТовар += ДАННЫЕ_JSON("id", СписокТоваров.maincode) + ",";
	ТекстЗапросаТовар += """alc_codes"": [""" + STRTRAN(СписокТоваров.alcocodes,",",""",""") + """]";
	ТекстЗапросаТовар += "}";
		
	ТекстЗапроса += ЕСЛИ(ПРАВСИМВ(ТекстЗапроса, 1)=="}",",","") + ТекстЗапросаТовар;
	
	ПРОПУСТИТЬ( 1, "СписокТоваров" );
}
IF (КОЛИЧЕСТВОСТРОК("СписокТоваров") > 0 )
{
	ТекстЗапроса += "]" + CHR(13) + CHR(10);
	ТекстЗапроса += "}";
}

//отправим запрос с алкокодами, если он не пустой
IF (!ПУСТО(ТекстЗапроса))
{
	ТОКЕН = RESTAPI.ALCOUNIT_Авторизация(АдресСервера, ЛОГИН, ПАРОЛЬ);
	IF ( _ЗАМЕНЯТЬАЛКОКОДЫВТОВАРАХ == false )
		Ответ = RESTAPI.ALCOUNIT_POSTЗАПРОС(АдресСервера, "/products", ТекстЗапроса, ТОКЕН, "");
	ELSE
		Ответ = RESTAPI.ALCOUNIT_PUTЗАПРОС(АдресСервера, "/products", ТекстЗапроса, ТОКЕН, "");
		
	ТекстЗапроса = "";
	//IF (!ПУСТО(Ответ))
	//	СООБЩЕНИЕ("Ответ сервера Alcounit:" + CHR(13) + CHR(10) + Ответ);
}

УДАЛИТЬКОНТЕКСТ("СписокТоваров");
