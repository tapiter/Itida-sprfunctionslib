
IF ( ПУСТО( СЕРТИФИКАТ ) )
{
	СЕРТИФИКАТ			= ЗАПРОС( "SELECT value FROM param_ex WHERE param= 'MARK_CERTIFICATE" + ПРЕДПРИЯТИЕ + "'" );
	ТЕСТОВЫЙКОНТУР		= VAL( ЗАПРОС( "SELECT value FROM param_ex WHERE param= 'MARK_APITEST" + ПРЕДПРИЯТИЕ + "'" ) ) != 0 ;
}

// Авторизуемся
МАРКИРОВКАТОКЕН			=  Маркировка.Авторизация( ПРЕДПРИЯТИЕ, СЕРТИФИКАТ, ТЕСТОВЫЙКОНТУР );
IF ( ПУСТО( МАРКИРОВКАТОКЕН ) ) RETURN false;

АдресСервера 			= ЕСЛИ( ТестовыйКонтур, "https://demo.lp.crpt.tech", "https://ismp.crpt.ru" );
Ответ					= "";
try
{
	ТекстОшибки			= "Сервер " + АдресСервера + CHR( 13 ) + "вернул сообщение об ошибке: " + CHR( 13 );
	Соединение 			= HTTPCONNECT( АдресСервера, "", true, МАРКИРОВКА.ФАЙЛЖУРНАЛА( ) );
	IF ( !МАРКИРОВКА.ПРОВЕРКАНАОШИБКИ( @Ответ ) ) THROW ( Ответ );
	
	Заголовки[ 0 ]		= "Content-Type: application/json";
	Заголовки[ 1 ]		= "Authorization: Bearer " + МАРКИРОВКАТОКЕН;
	Ответ				= HTTPGET( Соединение, "/api/v3/facade/doc/" + ИДДОКУМЕНТА + "/body?limit=10000", "Заголовки" );
	Ошибка				= !МАРКИРОВКА.ПРОВЕРКАНАОШИБКИ( @Ответ );
	КоличествоОшибок	= 0;
	ПОКА ( Ошибка && КоличествоОшибок < 10 )
	{
		Ответ			= HTTPGET( Соединение, "/api/v3/facade/doc/" + ИДДОКУМЕНТА + "/body?limit=10000", "Заголовки" );
		Ошибка			= !МАРКИРОВКА.ПРОВЕРКАНАОШИБКИ( @Ответ );
		КоличествоОшибок++; ОЖИДАНИЕ( 1000 );
	}
	IF ( Ошибка ) THROW ( Ответ );
}
catch ( ТекстСообщения )
{
	HTTPCLOSE( Соединение );
	СООБЩЕНИЕ( ТекстСообщения );
		
	RETURN "";
}
HTTPCLOSE( Соединение );

RETURN Ответ;
