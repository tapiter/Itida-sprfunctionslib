
Склад				= КОДУТМ;
ЕГАИС.ИнициализироватьОбмен( );

//------- Опредяляем общие реквизиты чека
ЕГАИССКЛАД			= ЗАПРОС( "SELECT value FROM param_ex WHERE param = 'EGAIS_SKLAD" + КОДУТМ + "'" );
ЕГАИСФИРМА			= ЗАПРОС( "SELECT value FROM param_ex WHERE param = 'EGAIS_FIRM" + КОДУТМ + "'" );

ЧЕКИНН				= ЗАПРОС( "SELECT inn FROM sprfirm WHERE code = '" + ЕГАИСФИРМА + "'" );
ЧЕККПП				= ЗАПРОС( "SELECT kpp FROM sprskl WHERE code = '" + ЕГАИССКЛАД + "'" );
IF ( ПУСТО( ЧЕККПП ) )
	ЧЕККПП			= ЗАПРОС( "SELECT kpp FROM sprfirm WHERE code = '" + ЕГАИСФИРМА + "'" );
ЧЕКНАИМЕНОВАНИЕ		= ЗАПРОС( "SELECT name FROM sprskl WHERE code = '" + ЕГАИССКЛАД + "'" );
ЧЕКАДРЕС			= ЗАПРОС( "SELECT address FROM sprskl WHERE code = '" + ЕГАИССКЛАД + "'" );
IF ( ПУСТО( ЧЕКАДРЕС ) )
	ЧЕКАДРЕС		= ЗАПРОС( "SELECT adress FROM sprfirm WHERE code = '" + ЕГАИСФИРМА + "'" );

ЧЕККАССА			= ЗАПРОС( "SELECT value FROM param_ex WHERE param = 'FR_SERIAL" + КОДОБОРУДОВАНИЯ + "'" );
IF ( ПУСТО( ЧЕККАССА ) )
	ЧЕККАССА		= ЗАПРОС( "SELECT serialnumber FROM sprequipment WHERE code = '" + КОДОБОРУДОВАНИЯ + "'" );
ЧЕКСМЕНА			= НОМЕРСМЕНЫ;
ЧЕКНОМЕР			= НОМЕРЧЕКА;

ЧЕКИНН				= ЕГАИС.ЗаменитьСимволы( ЧЕКИНН );
ЧЕККПП				= ЕГАИС.ЗаменитьСимволы( ЧЕККПП );
ЧЕКНАИМЕНОВАНИЕ		= LEFT( ЕГАИС.ЗаменитьСимволы( ЧЕКНАИМЕНОВАНИЕ ), 128 );
ЧЕКАДРЕС			= ЕГАИС.ЗаменитьСимволы( ЧЕКАДРЕС );
ЧЕККАССА			= ЕГАИС.ЗаменитьСимволы( ЧЕККАССА );
ЧЕКДАТАВРЕМЯ		= LEFT( STRTRAN( STRTRAN( STRTRAN( TTOC( ДАТАВРЕМЯ(), 0, "." ), " ", "" ), ":", "" ), ".", "" ), 10 );

ДОБАВИТЬКОНТЕКСТ( "SELECT sprnn.nnvid, temp.cena, sprnn.name, sprnn.a_proc, ISNULL( (SELECT MAX( d_litr ) FROM sprres_egais WHERE code = sprnn.nn), sprnn.d_litr ) AS d_litr, 
						  (SELECT TOP 1 bc FROM sprnnbc WHERE nn= sprnn.nn) AS ean, temp.kolp, temp.barcode, dbo.fn_alcotype( sprnn.nnvid ) AS alcotype
				   FROM " + ИмяТаблицы + " temp
				   INNER JOIN sprnn sprnn ON temp.nn = sprnn.nn
				   WHERE dbo.fn_alcotype( sprnn.nnvid ) = 1", "СтрокиЧека" );
IF ( КОЛИЧЕСТВОСТРОК( "СтрокиЧека" ) == 0 )	RETURN "Нет данных";

ТекстЧека		= 
"<?xml version=""1.0"" encoding=""UTF-8""?>
<Cheque inn=""" + ЧЕКИНН + """ kpp=""" + ЧЕККПП + """ address=""" + ЧЕКАДРЕС + """ name=""" + ЧЕКНАИМЕНОВАНИЕ + """ kassa= """ + ЧЕККАССА + """ shift= """ + ЧЕКСМЕНА + """ number= """ + ЧЕКНОМЕР + """ datetime=""" + ЧЕКДАТАВРЕМЯ + """>";

// Проверяем данные чека
IF ( ПУСТО( ЧЕКИНН ) )
{
	СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Не указан ИНН организации." );
	RETURN "";
}
IF ( !ПУСТО( ЧЕККПП ) AND ДЛИНА( ЧЕККПП ) != 9 )
{
	СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Неверно указан КПП организации." );
	RETURN "";
}
IF ( ПУСТО( ЧЕКАДРЕС ) )
{
	СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Не указан адрес магазина." );
	RETURN "";
}
IF ( ПУСТО( ЧЕКНАИМЕНОВАНИЕ ) )
{
	СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Не указано наименование магазина." );
	RETURN "";
}
IF ( ПУСТО( ЧЕККАССА ) )
{
	СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Не указан серийный номер ФР в карточке оборудования ФР." );
	RETURN "";
}
IF ( ПУСТО( ЧЕКСМЕНА ) )
{
	СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Не указан номер кассовой смены." );
	RETURN "";
}
IF ( ПУСТО( ЧЕКНОМЕР ) )
{
	СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Не указан номер чека." );
	RETURN "";
}


WHILE ( !КОНЕЦКОНТЕКСТА( "СтрокиЧека" ) )
{
	ЧЕКШКТОВАРА		= ЕСЛИ( ПУСТО( СтрокиЧека.ean ), ПЕРЕМЕННАЯ( "_ЕГАИСЗАМЕСТИТЕЛЬШКТОВАРА", "" ), СтрокиЧека.ean );
	IF ( ПУСТО( ЧЕКШКТОВАРА ) )
	{
		СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Для товара " + СтрокиЧека.name + " не указан штриховой код EAN." );
		RETURN "";
	}
	IF ( ROUND( СтрокиЧека.cena, 2 ) == 0 )
	{
		СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Для товара " + СтрокиЧека.name + " не указана цена." );
		RETURN "";
	}
	IF ( ROUND( СтрокиЧека.d_litr, 4 ) == 0 )
	{
		СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Для товара " + СтрокиЧека.name + " не указан объем тары." );
		RETURN "";
	}
	IF ( alcotype == 1 )
		ТекстЧека		+=  "<Bottle price=""" + STR( СтрокиЧека.cena, 10, 2 ) + """ barcode=""" + ЕГАИС.ЗаменитьСимволы( СтрокиЧека.barcode ) + """ ean=""" + ЧЕКШКТОВАРА + """ volume=""" + STR( СтрокиЧека.d_litr, 10, 4 ) + """ />";
	ELSE
	{
		IF ( ROUND( СтрокиЧека.a_proc, 4 ) == 0 )
		{
			СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Для товара " + СтрокиЧека.name + " не указана крепость." );
			RETURN "";
		}
		IF ( ROUND( СтрокиЧека.kolp, 0 ) == 0 )
		{
			СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Для товара " + СтрокиЧека.name + " не указано количество." );
			RETURN "";
		}
		IF ( ПУСТО( СтрокиЧека.nnvid ) )
		{
			СООБЩЕНИЕ( "Ошибка проверки чека." + CHR( 13 ) + "Для товара " + СтрокиЧека.name + " не указан код вида продукции." );
			RETURN "";
		}
		
		ТекстЧека		+=  "<nopdf code= """ + СтрокиЧека.nnvid + """ bname=""" + ЕГАИС.ЗаменитьСимволы( СтрокиЧека.name ) + """ volume=""" + STR( СтрокиЧека.d_litr, 10,4 ) + """ alc=""" + STR( СтрокиЧека.a_proc, 10, 4 ) + """ price=""" + STR( СтрокиЧека.cena, 10, 2 ) + """ ean=""" + ЧЕКШКТОВАРА + """ count=""" + STR( СтрокиЧека.kolp, 16, 0 ) + """/>";
	}
	
	ПРОПУСТИТЬ( 1, "СтрокиЧека" );
}
УДАЛИТЬКОНТЕКСТ( "СтрокиЧека" ); 

ТекстЧека		+= "</Cheque>";
			
// Отправляем получившийся текст на сервер
ОтветСервера = "";
IF ( !ЕГАИС.HTTPCommand( "POST", "/xml", ПЕРЕКОДИРОВАТЬ( ТекстЧека, "ANSI", "UTF-8" ), @ОтветСервера, "check.xml" ) ) RETURN ""; 

RETURN ОтветСервера;
