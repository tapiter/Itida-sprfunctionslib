//Получить вес из ВЕСОВ БЕЗ ПЭ
try
{
	ДрайверВесов	= СОЗДАТЬОБЪЕКТ( "MassaKDriver100.Scales", 1 );
}
Catch()
{
	ДрайверВесов = false;
}
IF ( ТИП( "ДрайверВесов" ) <> "O" )
{
	СООБЩЕНИЕ( "Не удалось создать объект драйвера. " + CHR(13) + "Установите MACCA-K: Драйвер 100 для работы с весами." );
	_ОШИБКАВЫПОЛНЕНИЯ		= true;
	RETURN 0;
}

Вес							= 0.00;
// После этого возможен доступ к свойствам и методам
НомерПорта = VAL( ЗАПРОС("SELECT com_n FROM sprequipment WHERE code = '" + КОДОБОРУДОВАНИЯ + "'") );
IF ( ПУСТО(НомерПорта) OR НомерПорта == 0 )
{
	//если номер порта не заполнен, то подключаемся к весам по IP
	IPport = 5100;
	IPaddres = ALLTRIM( ЗАПРОС("SELECT IP_ADDR FROM sprequipment WHERE code = '" + КОДОБОРУДОВАНИЯ + "'") );
	IF (ПУСТО(IPaddres))
	{
		СООБЩЕНИЕ("Не установлены параметры соединения с весами. " + CHR(13) + "Укажите номер порта весов или IP адрес весов в карточке оборудования." + CHR(13) + 
		"Если весы подключены через Wi-Fi/Ethernet, то номер COM-порта необходимо установить равным 0 и указать IP адрес.");
		_ОШИБКАВЫПОЛНЕНИЯ		= true;
		RETURN 0;
	}
	НомерПорта = IPaddres + ":" + IPport;
}
ELSE
{
	//если номер порта указан
	НомерПорта = "COM"+НомерПорта;
}
ДрайверВесов.Connection = НомерПорта;
// Подключаемся к весам
IF ( ДрайверВесов.OpenConnection() == 0 )
{
	res = ДрайверВесов.ReadWeight();
	IF ( res == 0 )
	{
		res = ДрайверВесов.Division; //Обработка размерности одного деления весов
		КоэффПересчета = 1;
		IF ( res == 0 ) //милиграммы
			КоэффПересчета = 0.000001;
		ELSE IF ( res == 1 ) //граммы
			КоэффПересчета = 0.001;
		ELSE IF ( res == 2 ) //килограммы    
			КоэффПересчета = 1;
		
		ВесСтабилен = ДрайверВесов.Stable;
		ЗначениеВесаНаВесах = ДрайверВесов.Weight * КоэффПересчета;
		//получаем только стабилизированный вес
		IF ( ВесСтабилен == 1 )
		{
			Вес	= ЗначениеВесаНаВесах;
		}
		ELSE
		{
			Ответ = СООБЩЕНИЕ("Вес на весах ещё не стабилизировался. " + CHR(13) + "Все равно установить вес " + ALLTRIM(ЗначениеВесаНаВесах) + " кг?", "Получение веса с весов", 4);
			IF (Ответ == 6)
			{
				Вес = ЗначениеВесаНаВесах;
			}
		}
	}
	ELSE
	{
		СООБЩЕНИЕ("Ошибка чтения веса с весов: ReadWeight() = " + res );
	}
}
ELSE
{
		СООБЩЕНИЕ( "Не удалось подключится к весам. " + CHR(13) + "Проверьте соединение кабеля или параметры связи в карточке оборудования." );
		_ОШИБКАВЫПОЛНЕНИЯ	= true;
}
//закрываем соединение с весами
ДрайверВесов.CloseConnection();
//уничтожаем объект драйвера
ДрайверВесов				= false;
//возвращаем вес
RETURN Вес;
