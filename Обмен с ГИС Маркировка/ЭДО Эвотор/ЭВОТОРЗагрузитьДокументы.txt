
ТЕСТОВЫЙКОНТУР			= VAL( ЗАПРОС( "SELECT value FROM param_ex WHERE param= 'MARK_APITEST" + ПРЕДПРИЯТИЕ + "'" ) ) != 0 ;
ДАТАЗАГРУЗКИ			= CTOD( ЗАПРОС( "SELECT value FROM param_ex WHERE param= 'MARK_EDIDOWNLOADDATE" + ПРЕДПРИЯТИЕ + "'" ) );
IF ( РАЗНИЦАДАТ( ДАТАЗАГРУЗКИ, ДАТА( ), "day" ) > 30 )
	ДАТАЗАГРУЗКИ		= ДОБАВИТЬДНИ( ДАТА( ), -30 );

ЭВОТОРТОКЕН				= Маркировка.ЭВОТОРАвторизация( ПРЕДПРИЯТИЕ, СЕРТИФИКАТ );
IF ( ПУСТО( ЭВОТОРТОКЕН ) ) RETURN false;

IF ( !ДОБАВИТЬКОНТЕКСТ( "LOCAL: uri char, id char", "Загрузка" ) ) RETURN false;

АдресСервера 			= ЕСЛИ( ТЕСТОВЫЙКОНТУР, "https://edo-v2.platformaofd.ru", "https://edo.platformaofd.ru" );
Заголовки[ 0 ]			= "Authorization: Bearer " + ЭВОТОРТОКЕН;
Заголовки[ 1 ]			= "";

КоличествоДней			= РАЗНИЦАДАТ( ДАТАЗАГРУЗКИ, ДАТА( ), "day" ) + 1;
ИНДИКАТОР( "Загрузка документов", КоличествоДней );
// Проходим по дням периода и запрашиваем документы из Платформа ЭДО
try
{
	ТекстОшибки			= "Сервер " + АдресСервера + CHR( 13 ) + "вернул сообщение об ошибке: " + CHR( 13 );
	Соединение 			= HTTPCONNECT( АдресСервера, "", true, Маркировка.ФАЙЛЖУРНАЛА( ) );

	ТекущаяДата			= ДАТАЗАГРУЗКИ;
	КонечнаяДата		= ДОБАВИТЬДНИ( ДАТА( ) );
	КоличествоДней		= 1;	
	ПОКА( ТекущаяДата < КонечнаяДата )
	{
		ИНДИКАТОР( КоличествоДней, "Загрузка документов за " + DTOC( ТекущаяДата ) );
		ДатаНачала			= DTOC( ТекущаяДата, 7, "-" ) + "T00:00:00.0Z";
		ДатаКонца			= DTOC( ТекущаяДата, 7, "-" ) + "T23:59:59.999Z";
		Ответ				= HTTPGET( Соединение, "api/v2/documents?created_from=" + ДатаНачала + "&created_till=" + ДатаКонца + "&page=0&page_size=1000" + ЕСЛИ( ТОЛЬКОМАРКИРОВКА, "&labeled_goods=true", "" ), "Заголовки" );
		IF ( !МАРКИРОВКА.ЭВОТОРПРОВЕРКАНАОШИБКИ( @Ответ ) ) THROW ( Ответ );
/**********************************************/
// УДАЛИТЬ
//		IF ( ДатаНачала == "2020-06-24T00:00:00.0Z" )
//		{
//			ФАЙЛ				= ФАЙЛОТКРЫТЬ( "D:\data.json" );
//			Ответ				= ФАЙЛПРОЧИТАТЬ( ФАЙЛ, "S", 50000 );
//			ФАЙЛЗАКРЫТЬ( ФАЙЛ );
//		}
/**********************************************/

		ЗАГРУЗИТЬJSON( "СписокДокументов", Ответ, "text" );
		ПЕРЕЙТИВНАЧАЛО( "СписокДокументов" );
		ПОКА ( !КОНЕЦКОНТЕКСТА( "СписокДокументов" ) )
		{
			ЗАГРУЗИТЬJSON( "Документ", ЗНАЧЕНИЕПОЛЯ( "СписокДокументов", "text", "" ) );
			ДокументИД		= ЗНАЧЕНИЕПОЛЯ( "Документ", "id", "" );
			ДокументПуть	= ПОЛЕ_JSON( ЗНАЧЕНИЕПОЛЯ( "Документ", "content", "" ), "uri", "" );
			
			IF ( !ПУСТО( ДокументИД ) && !ПУСТО( ДокументПуть ) )
			{
				// Проверяем наличие такого же документа в базе
				IF ( ЗАПРОС( "SELECT CASE WHEN EXISTS( SELECT * FROM mark_in WHERE guid= '" + STDF( ДокументИД ) + "' AND ent= '" + STDF( ПРЕДПРИЯТИЕ ) + "' ) THEN 1 ELSE 0 END" ) == 0 ) 
				{
					ДОБАВИТЬСТРОКИ( 1, "Загрузка" );
					ИЗМЕНИТЬПОЛЕ( "Загрузка", "id", ДокументИД );
					ИЗМЕНИТЬПОЛЕ( "Загрузка", "content", ДокументПуть );
				}
			}
			УДАЛИТЬКОНТЕКСТ( "Документ" );
			ПРОПУСТИТЬ( 1, "СписокДокументов" );
		}
		УДАЛИТЬКОНТЕКСТ( "СписокДокументов" );

		ТекущаяДата			= ДОБАВИТЬДНИ( ТекущаяДата );
		КоличествоДней++;
	}

	ИНДИКАТОР( "Создание документов", КОЛИЧЕСТВОСТРОК( "Загрузка" ) );
	
	Заголовки[ 1 ]			= "accept: application/xml";
	ВсегоДокументов			= 0;
	НомерДокумента			= 1;
	// Проходим по полученному списку документов и загружаем сами документы
	ПЕРЕЙТИВНАЧАЛО( "Загрузка" );
	ПОКА ( !КОНЕЦКОНТЕКСТА( "Загрузка" ) )
	{
		ИНДИКАТОР( НомерДокумента, "Создано документов " + STR( НомерДокумента ) + " из " + STR( КОЛИЧЕСТВОСТРОК( "Загрузка" ) ) );
		НомерДокумента++;
/**********************************************/
// УДАЛИТЬ
//		IF ( Загрузка.id == "6180504" )
//		{
//			ФАЙЛ				= ФАЙЛОТКРЫТЬ( "D:\data.xml" );
//			Ответ				= ФАЙЛПРОЧИТАТЬ( ФАЙЛ, "S", 50000 );
//			ФАЙЛЗАКРЫТЬ( ФАЙЛ );
//		}
//		ELSE
//		{
/**********************************************/
			Ответ				= HTTPGET( Соединение, "api/v2/documents/" + Загрузка.id + "/content", "Заголовки" );
			IF ( !МАРКИРОВКА.ЭВОТОРПРОВЕРКАНАОШИБКИ( @Ответ ) ) THROW ( Ответ );
/**********************************************/
// УДАЛИТЬ
//		}
/**********************************************/
		// Создаем документ из полученного файла
		СозданиеДокуменгта		= Маркировка.СоздатьДокументИЗXML( Предприятие, Загрузка.id, "", Ответ );
		IF ( ПУСТО( СозданиеДокуменгта ) ) THROW( "Ошибка загрузки данных из ЭДО." );
		IF ( СозданиеДокуменгта != "ДОКУМЕНТУЖЕЕСТЬ" ) ВсегоДокументов++;
		ПРОПУСТИТЬ( 1, "Загрузка" );
	}
	ИНДИКАТОР( );
	
	// Обновляем дату загрузки
	ЗАПРОС( "DELETE FROM param_ex WHERE param= 'MARK_EDIDOWNLOADDATE" + ПРЕДПРИЯТИЕ + "'" );
	ЗАПРОС( "INSERT INTO param_ex ( param, value ) VALUES ( 'MARK_EDIDOWNLOADDATE" + ПРЕДПРИЯТИЕ + "', '" + DTOC( ДОБАВИТЬДНИ( ТекущаяДата, -1 ) ) + "' )" );
	
	// Сообщаем о завершении загрузки
	IF ( ВсегоДокументов == 0 )
		СООБЩЕНИЕ( "Новые документы не найдены." );
	ELSE
		СООБЩЕНИЕ( "Успешно загружен(о) " + ПРОПИСЬ( ВсегоДокументов, "документ", "документа", "документов" ) );

	УДАЛИТЬКОНТЕКСТ( "Загрузка" );
}
catch ( ТекстСообщения )
{
	ИНДИКАТОР( );
	HTTPCLOSE( Соединение );
	СООБЩЕНИЕ( ТекстСообщения );
	УДАЛИТЬКОНТЕКСТ( "Загрузка" );
	RETURN false;
}
HTTPCLOSE( Соединение );

RETURN true;
